/*******************************************************************************
* Instrument: ESS_HEIMDAL
*
* %I
* Written by: Peter Willendrup and Jakob Garde
* Date: June 2019
* Origin: DTU
* %INSTRUMENT_SITE: ESS
*
* Simplistic ToF Diffractometer HEIMDAL@ESS
*
* %D
* <instrument description>
*
* Example: lam0=2.0, dlam=1.8
*
* %P
* lam0: [ÅÅ]          source median
* dlam: [ÅÅ]          source deviation
* samplerate_1: []    propability of hitting sample #1 (CoFe2O4)
* samplerate_2: []    propability of hitting sample #2 (CoFe), accumulated with samplerate_1
*
* %E
*******************************************************************************/
DEFINE INSTRUMENT ESS_HEIMDAL(lmax=4, lmin=0.5, samplerate_1=0.33, samplerate_2=0.66, T=400)


DECLARE
%{
  double tof,tofmin,tofmax;
  double det_th1, det_th2;
  double det_rad,det_Linst,det_t0,det_d1,det_d2;
  double hm = 2*PI*K2V; // h/m_n
  double samplechoice = 0;
  double Tc = 500;
  double lam0;
  double dlam;
%}

INITIALIZE
%{
// angular range
det_th1=0;
det_th2=180;
// dhkl range
det_d1=0.5;
det_d2=5.5;
det_rad=2;
det_Linst=150+det_rad;
tof=det_Linst/hm*lam0*1e6; // tof in [us]
tofmin=det_Linst/hm*lmin*1e6; // tof in [us]
tofmax=det_Linst/hm*lmax*1e6; // tof in [us]
lam0=(lmax+lmin)/2.0;
dlam=(lmax-lmin)/2.0;

if (samplerate_2 < samplerate_1) {
    printf("WARNING: samplerate_1 must be smaller than samplerate_2.");
    exit(1);
}
%}


TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

COMPONENT source = Source_simple(
    lambda0=lam0,
    dlambda=dlam,
    target_index=3,
    focus_xw=0.02,
    focus_yh=0.07,
    flux=1e14)
AT (0,0,0) RELATIVE PREVIOUS
EXTEND %{
  samplechoice=rand01();
%}

COMPONENT psd_source = PSD_monitor(
    filename="PSD_source.dat",
    nx=40,
    ny=40,
    xwidth=0.3,
    yheight=0.3,
    restore_neutron=1)
AT (0, 0, 0.01) RELATIVE source


//
// SAMPLE
//
SPLIT 32 COMPONENT SampleArm = Arm()
AT (0, 0, 150) RELATIVE source

// CoFe2O4, compound #1
COMPONENT sample_1 = PowderN_dspace_factor (
    dspace_factor=1,
    d_phi=10,
    radius=0.02,
    yheight=0.07,
    p_transmit=0,
    reflections = "CoFe2O4_109044.txt")
WHEN (samplechoice<samplerate_1)
AT (0, 0, 150) RELATIVE source

// CoFe, compound #2
COMPONENT sample_2 = PowderN_dspace_factor (
    dspace_factor=1,
    d_phi=10,
    radius=0.02,
    yheight=0.07,
    p_transmit=0,
    reflections = "CoFe_56273.txt")
WHEN (samplechoice>=samplerate_1 && samplechoice<samplerate_2)
AT (0, 0, 150) RELATIVE source

// magnetic peak
COMPONENT Magnet =  PowderN_dspace_factor (
    dspace_factor=1,
    d_phi=10,
    radius=0.02,
    yheight=0.07,
    p_inc=0,
    p_transmit=0,
    reflections = "magnetic_peak.txt")
WHEN (samplechoice>=samplerate_2)
AT (0, 0, 150) RELATIVE source
EXTEND %{
    // diminishes peak intensity
    if (mcipT<Tc && mcipT>=0) {
      p *= (Tc-mcipT)/(mcipT);
    }
    else
        ABSORB;
%}


//
// DETECTOR
//


COMPONENT npi_tof_dhkl_detector = NPI_tof_dhkl_detector(
    nd=3000,
    filename="dhkl.dat",
    yheight=1.0,
    zdepth=0.01,
    radius=det_rad,
    amin=det_th1,
    amax=det_th2,
    d_min=det_d1,
    d_max=det_d2,
    time0=0,
    Linst=152,
    Lc=0,
    res_x=0.002,
    res_y=0.005,
    res_t=1e-6,
    mu=1.0,
    modulation=0,
    mod_dt=0,
    mod_twidth=0,
    mod_shift=0,
    mod_d0_table="dhkl.tab",
    restore_neutron=1)
AT (0, 0, 0) RELATIVE sample_1


// ToF vs. 2theta map
COMPONENT psdtof = NPI_tof_theta_monitor(
    nt = 800, na = 600, filename = "tof_theta.dat",
    radius = 2, yheight = 1, tmin = 0.95*tofmin, tmax = tofmax*1.05,
    amin = det_th1, amax = det_th2, restore_neutron = 1,verbose=0)
AT (0, 0, 0) RELATIVE sample_1

// ToF vs. 2theta map, detail
COMPONENT psdtofDetail= NPI_tof_theta_monitor(
    nt = 400, na = 400, filename = "tof_theta_detail.dat",
    radius = 2, yheight = 1, tmin = 100e3, tmax = 110e3,
    amin = 75, amax = 80, restore_neutron = 1,verbose=0)
AT (0, 0, 0) RELATIVE sample_1



END
