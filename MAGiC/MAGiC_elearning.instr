DEFINE INSTRUMENT MAGiC_half_polarized(spectrum=0, high_res=0, coll_set=0.3, sample_rot)

DECLARE
%{
    double lfeed, dfeed_chop;
    double dchop_guide, l1;
    double rot1, rot2;
    double l3,l4,l5,l6,l7;
    double xaxis, yaxis;
    double WaveMin, WaveMax, Lambda0, dLambda;
    double sample_dist;
    double inx, iny, outx, outy, inxfeed, inyfeed, slity,slitx;
    double temp1, temp2;
    
    double bend_x, bend_ang;
    double Linx=4, Loutx=1.9, Liny=4.3, Louty=1.5, rot1=0.2344, pol=0;
    double flight_min,flight_max;
    int nchan=6;
    double l2=3;

    int coldornot;

    double elliptic_dim_calc(double Fin, double Fout, double bb, double length){
        double ah, bh, ch, xh;
        
        bh=bb/2;
        ch = (Fin+Fout+length)/2.;
        ah = sqrt(bh*bh+ch*ch);
        xh = -ch+Fin;

        temp1 = bh*bh*(1.-(xh*xh/(ah*ah)));
        temp1 = 2*sqrt(temp1);
        xh=xh+length;
        temp2 = bh*bh*(1.-(xh*xh/(ah*ah)));
        temp2 = 2*sqrt(temp2);  
    }
%}

INITIALIZE
%{
    if(spectrum == 1){
        coldornot = 1;
    }
    else{
        coldornot = 0;
    }

	dchop_guide=0.5;
    l1=74;
    l3=67+1.5;
    l4=4;
    l5=0.5;
    l6=0.5;
    l7=0.5;
    
    xaxis=0.08;
    yaxis=0.08;
    
    sample_dist=1.5;
    
    if(coldornot==0) 
    {
        WaveMin=0.2;
        WaveMax=6;
        flight_min = 160.4*0.6/3956;
        flight_max = 160.4*2.3/3956;
    }
    else
    {
        WaveMin=0.2;
        WaveMax=6;
        flight_min = 160.4*2.0/3956;
        flight_max = 160.4*3.7/3956;
    }
    
    nchan= round(nchan);
    
    // First ellipse calculations
    elliptic_dim_calc(Linx,Loutx+l2+l3,xaxis,l1);
    inx = temp2;
    slitx=temp1;
    elliptic_dim_calc(Liny,Louty+l2+l3,yaxis,l1);
    iny = temp2;
    slity=temp1;
    
    // Second ellipse calculations
    elliptic_dim_calc(Linx+l1+l2,Loutx,xaxis,l3);
    outx = temp2;
    elliptic_dim_calc(Liny+l2+l1,Louty,yaxis,l3);
    outy = temp2;
%}

TRACE
	COMPONENT Origin = Progress_bar() 
	AT (0,0,0) ABSOLUTE

	/****************************************************
	/*
	/*        Source and monolith elements
	/*
	/****************************************************/
	// ESS butterfly moderator: 5MW
	COMPONENT source = ESS_butterfly(
	    sector="W", beamline=6, yheight=0.03,
	    Lmin=WaveMin, Lmax=WaveMax, cold_frac=coldornot, target_index=3,
	    focus_xw=0.035, focus_yh=0.03)
	AT (0, 0, 0) RELATIVE Origin
	ROTATED (0,0,0) RELATIVE Origin
	
	// Initial vertical kink (0.5°)
    COMPONENT source_rot = Arm()
    AT(-0.034,0,0) RELATIVE source
    ROTATED (2*rot1,0,0) RELATIVE source
    	
    // Beam extraction
    COMPONENT Beam_ext_1= Slit(
		xwidth=0.124, yheight=0.03)
	AT (0.034,0,1.898) RELATIVE source_rot
	
	COMPONENT Beam_ext_3= Slit(
		xwidth=0.035, yheight=0.03)
	AT (0,0,5.888) RELATIVE source_rot

    COMPONENT Pol_bend = Pol_bender(
        xwidth=0.035, yheight=0.03,
        length=0.05, radius=180*0.05/(0.7*PI),
        nslit=233, d=0.000001, endFlat=0, drawOption=3,
        rTopUpPar=    {0.00, 0.0219, 6.07, 3.80, 0.003},
        rTopDownPar=  {0.00, 0.0219, 6.07, 3.80, 0.003},
        rBotUpPar=    {0.00, 0.0219, 6.07, 3.80, 0.003},
        rBotDownPar=  {0.00, 0.0219, 6.07, 3.80, 0.003},
        rLeftUpPar=   {0.99, 0.0219, 6.07, 3.80, 0.003},
        rLeftDownPar= {0.99, 0.0219, 6.07, 3.80, 0.003},
        rRightUpPar=  {0.99, 0.0219, 6.07, 3.80, 0.003},
        rRightDownPar={0.99, 0.0219, 6.07, 3.80, 0.003})
    WHEN spectrum == 1
    AT (0, 0, 5.888) RELATIVE source_rot
    ROTATED (0,-0.7,0) RELATIVE source

    // Pulse Shaping Chopper emulation block
    COMPONENT PSC_therm = Arm()
    WHEN spectrum == 0
    AT (0, 0, 6.24) RELATIVE source_rot
    EXTEND
    %{
        if(t < 6.24*0.6/3956.0){
            ABSORB;
        }
        if(t > 6.24*2.3/3956.0+0.00286){
            ABSORB;
        }
    %}

    COMPONENT PSC_cold = Arm()
    WHEN spectrum == 1
    AT (0, 0, 0.00001) RELATIVE PREVIOUS
    EXTEND
    %{
        if(t < 6.24*2.0/3956.0){
            ABSORB;
        }
        if(t > 6.24*3.7/3956.0+0.00286){
            ABSORB;
        }
    %}

    COMPONENT PSC_HR = Arm()
    WHEN (high_res == 1)
    AT (0, 0, 0.00001) RELATIVE PREVIOUS
    EXTEND
    %{
        if(t < 6.24*0.6/3956.0+0.00266){
            ABSORB;
        }
    %}    
    /****************************************************
	/*
	/*           Neutron transport system
	/*
	/****************************************************/
	// First elliptical element
    COMPONENT el1= Elliptic_guide_gravity(
		l=l1, linxw=Linx, linyh=Liny, loutxw=Loutx+l2+l3, loutyh=Louty+l2+l3,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0,6.9) RELATIVE source_rot
	ROTATED (0,0,0) RELATIVE source_rot
    
	COMPONENT el1_out= Arm()
	AT (0,0,l1 + 0.005) RELATIVE PREVIOUS

    // Band Chopper emulation block
    COMPONENT BC_therm = Arm()
    WHEN spectrum == 0
    AT (0, 0, 0) RELATIVE PREVIOUS
    EXTEND
    %{
        if(t < 80.90*0.6/3956.0){
            ABSORB;
        }
        if(t > 80.90*2.3/3956.0+0.00286){
            ABSORB;
        }
    %}

    COMPONENT BC_cold = Arm()
    WHEN spectrum == 1
    AT (0, 0, 0) RELATIVE PREVIOUS
    EXTEND
    %{
        if(t < 80.90*2.0/3956.0){
            ABSORB;
        }
        if(t > 80.90*3.7/3956.0+0.00286){
            ABSORB;
        }
    %}

	// First downward kink (0.25°)
	COMPONENT el1_s_rot = Arm()
	AT (0,0,0) RELATIVE PREVIOUS
	ROTATED (-rot1,0,0) RELATIVE PREVIOUS
 
 	// Straigth element
	COMPONENT st1= Guide_gravity(
		w1=xaxis, h1=yaxis,
		w2=xaxis, h2=yaxis, l=l2, d=0.0003,
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003,
		nhslit=nchan)
	AT (0,0,0) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS
    
	COMPONENT st1_out= Arm()
	AT (0,0,l2 + 0.005) RELATIVE PREVIOUS
	
    // Second downward kink (0.25°)
    // Guide is now horizontal
    COMPONENT s_el2_rot= Arm()
	AT (0,0,0) RELATIVE PREVIOUS
	ROTATED (-rot1,0,0) RELATIVE PREVIOUS

	// Second elliptical element
	COMPONENT el2= Elliptic_guide_gravity(
		l=l3, linxw=Linx+l2+l1, linyh=Liny+l1+l2, loutxw=Loutx+7, loutyh=Louty+7,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0, 0) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS

    // First collimation slit
    COMPONENT collim_1 = Slit(
        xwidth=(l4+l5+l6+l7+1.5)*2*coll_set*PI/180.0, yheight=(l4+l5+l6+l7+1.5)*2*coll_set*PI/180.0)
    AT (0,0,l3+0.001) RELATIVE PREVIOUS
    
	COMPONENT el3= Elliptic_guide_gravity(
		l=l4, linxw=Linx+l2+l1+l3, linyh=Liny+l1+l2+l3, loutxw=Loutx+l7+l6+l5, loutyh=Louty+l7+l6+l5,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0,0.001) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS

	// Second collimation slit
    COMPONENT collim_2 = Slit(
        xwidth=(l5+l6+l7+1.5)*2*coll_set*PI/180.0, yheight=(l5+l6+l7+1.5)*2*coll_set*PI/180.0)
    AT (0,0,l4+0.001) RELATIVE PREVIOUS
    
	COMPONENT el4= Elliptic_guide_gravity(
		l=l5, linxw=Linx+l2+l1+l3+l4, linyh=Liny+l1+l2+l3+l4, loutxw=Loutx+l7+l6, loutyh=Louty+l7+l6,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0,0.001) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS
	
	// Third collimation slit
    COMPONENT collim_3 = Slit(
        xwidth=(l6+l7+1.5)*2*coll_set*PI/180.0, yheight=(l6+l7+1.5)*2*coll_set*PI/180.0)
    AT (0,0,l5+0.001) RELATIVE PREVIOUS

	COMPONENT el5= Elliptic_guide_gravity(
		l=l6, linxw=Linx+l2+l1+l3+l4+l5, linyh=Liny+l1+l2+l3+l4+l5, loutxw=Loutx+l7, loutyh=Louty+l7,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0,0.001) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS
	
	// Fourth collimation slit
    COMPONENT collim_4 = Slit(
        xwidth=(l7+1.5)*2*coll_set*PI/180.0, yheight=(l7+1.5)*2*coll_set*PI/180.0)
    AT (0,0,l6+0.001) RELATIVE PREVIOUS

	COMPONENT el6= Elliptic_guide_gravity(
		l=l7, linxw=Linx+l2+l1+l3+l4+l5+l6, linyh=Liny+l1+l2+l3+l4+l5+l6, loutxw=Loutx, loutyh=Louty,
		xwidth = xaxis, yheight = yaxis, dimensionsAt="mid",
		R0 = 0.99,Qc=0.0219,alpha=6.07,m=4.0,W=0.003)
	AT (0,0,0.001) RELATIVE PREVIOUS
	ROTATED (0,0,0) RELATIVE PREVIOUS

	// End of the guide system
    COMPONENT el2_out= Arm()
	AT (0,0,l7+0.005) RELATIVE PREVIOUS
	
    // Sample position
	COMPONENT sample_pos= Arm()
	AT (0,0,sample_dist) RELATIVE el2_out

	/****************************************************
	/*
	/*        Beam monitoring at sample position
	/*
	/****************************************************/
	// Check brilliance
	COMPONENT L_out = L_monitor(
		xwidth=0.005,yheight=0.005,
		nL=101, filename="L_out.txt", Lmin=WaveMin, Lmax=WaveMax)
	AT (0,0,0) RELATIVE PREVIOUS
	
	// Control beam shape
	COMPONENT Mon_out2 = PSD_monitor(
		nx = 201, ny = 201, filename = "Mon_out2.txt", xwidth = 0.03,
		yheight = 0.03)
	AT (0, 0, 0) RELATIVE PREVIOUS

    // Single crystal to work with
    SPLIT 1000 COMPONENT sample = Single_crystal(
        radius = 0.01, reflections = "C60.lau", mosaic=30,
        ax=0, ay=0, az=14.408,
        bx=14.408, by=0, bz=0,
        cx=0, cy=14.408, cz=0)
    AT (0, 0, 0) RELATIVE PREVIOUS
    ROTATED(0, sample_rot, 0) RELATIVE PREVIOUS

    // Q-space detector in a single plane
    COMPONENT Detector = PSD_MAGiC(
        sample_rot = sample_rot, a_param=14.408, nx = 501, ny = 17, nt = 357, filename = "TOF",
        xmin=10, xmax=70, ymin=-3, ymax=3, tmin=flight_min*1e6, tmax=flight_max*1e6, radius=1,
        lambda_calc = high_res)
    AT (0,0,0+0.00001) RELATIVE Mon_out2
END
